@DECL

VerticeXMLTree(
	L("out"),
	L("n"),
	L("parent id")
	)
{
L("childs") = pndown(L("n"));
L("leaf") = 0;

while (L("childs")) {
	L("name") = pnname(L("childs"));
	if (strlength(L("name")) > 1) {
		L("id") = G("id")++;
		L("tag") = strtrim(strpiece(L("name"),0,strlength(L("name"))-1));

		L("out") << "<vertice";
		VerticeGetAttributes(L("out"),L("childs"));
		L("out") << ">\n";
		L("out") << "  <id>" << L("id") << "</id>\n";
		L("out") << "  <label>" << L("tag") << "</label>\n";
		L("out") << "</vertice>\n";

		if (L("parent id")) {
			L("out") << "<edge>\n";
			L("out") << "  <source>" << L("parent id") << "</source>\n";
			L("out") << "  <target>" << L("id") << "</target>\n";
			L("out") << "</edge>\n";			
		}

		if (pndown(L("childs"))) {
			VerticeXMLTree(L("out"),L("childs"),L("id"));
		}
	}

	L("childs") = pnnext(L("childs"));
}
return L("leaf");
}

VerticeXMLRecurseAll(
	L("out"),
	L("n"),	     # Current node.
	L("level")   # level of recursion
	)
{
L("childs") = pndown(L("n"));
L("leaf") = 0;

while (L("childs")) {
	L("name") = pnname(L("childs"));
	if (strlength(L("name")) > 1) {
		L("tag") = strpiece(L("name"),1,strlength(L("name"))-1);
		if (pndown(L("childs"))) {
			G("id")++;
			L("out") << "\n" << VerticeSpacing(L("level")) << "<" << L("tag");
			VerticeGetAttributes(L("out"),L("childs"));
			L("out") << ">";
			if (!VerticeXMLRecurseAll(L("out"),L("childs"),L("level")+1))
				L("out") << "\n" << VerticeSpacing(L("level"));
			L("out") << "</" << L("tag") << ">";
		} else {
			L("out") << pnvar(L("childs"),"$text");
			L("leaf") = 1;
		}
	}

	L("childs") = pnnext(L("childs"));
}
return L("leaf");
}

VerticeGetAttributes(L("out"),L("n")) {
	VerticeAddAttribute(L("out"),L("n"),"stem");
	VerticeAddAttribute(L("out"),L("n"),"voice");
	VerticeAddAttribute(L("out"),L("n"),"tense");
	VerticeAddAttribute(L("out"),L("n"),"aspect");
}

VerticeAddAttribute(L("out"),L("n"),L("name")) {
	L("value") = pnvar(L("n"),L("name"));
	if (L("value"))
  		L("out") << " " << L("name") << "=\"" << L("value") << "\"";
}

VerticeSpacing(L("num")) {
	L("i") = 1;
	L("VerticeSpacing") = " ";
	while (L("i")++ < L("num")) {
	   L("VerticeSpacing") = L("VerticeSpacing") + "   ";
	}
	return L("VerticeSpacing");
}